:subtitle: Boolean Algebras, VHDL, and forth
:icons: font 
ifdef::env-github[:hardbreaks:]
ifdef::env-github[:toc:]
ifdef::env-github[:toclevels: 3]

:relfileprefix: ../
:dir: blog/
include::{relfileprefix}README.adoc[tag=preamble]

:relfileprefix:

== {subtitle}

.nand2tetris Figure 1.6
link:https://creativecommons.org/licenses/by-nc-sa/3.0/[CC BY-NC-SA 3.0]

image::VHDL.png[]

=== HDL program

==== using graphiz

<<graphiz>>

[source,dot]
----
digraph G {

a -> nota # NOT
b -> notb # NOT

{a notb} -> w1 # AND
{nota b} -> w2 # AND

{w1 w2} -> out # OR

}
----

==== using a made-up DSL

taking inspiration from graphiz and forth

[source,factor]
----
:: xor ( a b -- out )
     a not :> nota
     b not :> notb

a notb and :> w1
nota b and :> w2

w1 w2  or  :> out

out
;
----

actually this is executable `factor` code, using <<lexical-variables>>, so it isn't super "made-up".

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

****

here's the entire thing...

.without using intermediate variables: 

[source,factor]
----
:: xor ( a b -- out )
a b not and
b a not and
or
;
----

.without using any variables: 

[source,factor]
----
: xor ( a b -- out )
[      not and ]
[ swap not and ]
2bi
or
;
----

********

Note that `a` and `b` disappeared from the definition,
and are just left in the stack notation.
The stack notation can be thought of as being similar to
type signatures/annotations in other languages, eg:

[source,haskell]
----
xor :: a -> b -> out
----

In this example we manipulated the stack using the dataflow combinator <<two-bi>>,
and the <<shuffle-word>> `swap` (which has the stack effect `( a b -- b a )`)

********

.without using quotations: 

[source,factor]
----
: xor  ( a b -- out )
2dup ! ( a b -- a b a b )
    not and ! ( a b a b -- a b w1 )
-rot                   ! ( a b w1 -- w1 a b )
    swap                         ! ( w1 a b -- w1 b a )
    not and                                ! ( w1 b a -- w1 w2 )
    or
;
----

********

Here, instead of using `2bi` we have to use `2dup` to duplicate `a` and `b`,
 since we're going to use them twice.
We then use `-rot` to move `w1` out of the way for `[swap not and]`,
 only that this time they're not in a quotation.
Think of `2bi` as a higher-order function,
 and by using `2dup ... -rot`
 we remove the need for higher-order functions.
I tried to align the stack annotations to make clearer what is going on.

********

****

=== Test script

[source,factor,line-comment=!]
----
IN: testing-xor
USING: math.combinatorics prettyprint kernel sequences continuations arrays locals ;

:: xor1 ( a b -- out )
     a not :> nota
     b not :> notb
a notb and :> w1
nota b and :> w2
w1 w2  or  :> out
out
;
:: xor2 ( a b -- out ) a b not and b a not and or ;
 : xor3 ( a b -- out ) [ not and ] [ swap not and ] 2bi or ;
 : xor4 ( a b -- out ) 2dup not and -rot swap not and or ;
 : xor5 ( a b -- out ) 2dup swap [ not and ] 2bi@ or ;       ! <1>

: possible-inputs ( -- seq )
{ { f f }
  { t f }
  { f t }
  { t t } }
;

! could more easily be defined as:
! : possible-inputs2 ( -- seq ) { f t } 2 all-selections ; ! <2>

: (test) ( seq quot -- seq ) dupd with-datastack 2array ;
: test ( seq quot -- seq ) [ (test) ] curry map ;          ! <3> <4>

possible-inputs [ xor1 ] test .
possible-inputs [ xor2 ] test .
possible-inputs [ xor3 ] test .
possible-inputs [ xor4 ] test .
possible-inputs [ xor5 ] test .

----

.output: same for all 5 `xor` implementations
[source,factor,line-comment=!]
----
{
    { { f f } { f } }
    { { t f } { t } }
    { { f t } { t } }
    { { t t } { f } }
}
----

.new words:
<1> <<two-bi-at>>
<2> <<all-selections>> using it to generate all possible combinations of `true` and `false`
<3> <<curry>>          using it to pass `xor` to `map` which in turn passes `xor` to `(test)`
<4> <<map>>            using it to test `xor` on each input, and capture the ouput


[appendix]
== Tools and words

[horizontal]
[[graphiz]]         graphiz playground :: http://dreampuf.github.io/GraphvizOnline/#digraph%20G%20%7B%0A%0Aa%20-%3E%20nota%20%23%20NOT%0Ab%20-%3E%20notb%20%23%20NOT%0A%0A%7Ba%20notb%7D%20-%3E%20w1%20%23%20AND%0A%7Bnota%20b%7D%20-%3E%20w2%20%23%20AND%0A%0A%7Bw1%20w2%7D%20-%3E%20out%20%23%20OR%0A%0A%7D%0A
[[lexical-variables]] lexical variables:: https://docs.factorcode.org/content/article-locals-examples.html
[[two-bi]]                        `2bi`:: https://docs.factorcode.org/content/word-2bi%2Ckernel.html
[[shuffle-word]]           shuffle word:: https://docs.factorcode.org/content/article-shuffle-words.html
[[two-bi-at]]                    `2bi@`:: link:++https://docs.factorcode.org/content/word-2bi__at__,kernel.html++[]
[[all-selections]]     `all-selections`:: https://docs.factorcode.org/content/word-all-selections,math.combinatorics.html
[[curry]]                       `curry`:: https://docs.factorcode.org/content/word-curry,kernel.html
[[map]]                           `map`:: https://docs.factorcode.org/content/word-map,sequences.html
